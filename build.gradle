plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'com.palantir.docker' version '0.25.0'
    id "org.owasp.dependencycheck" version "8.2.1"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'kotlin-kapt'

group 'com.exactpro.th2'
version release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()

    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.5.0')

    implementation("com.exactpro.th2:common:5.4.0-dev") {
        exclude group: 'com.exactpro.th2', module: 'task-utils'
    }
    implementation group: 'com.exactpro.th2', name: 'common-utils', version: '2.2.1-dev'

    implementation 'com.exactpro.th2:netty-bytebuf-utils:0.0.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'

    implementation 'com.exactpro.th2:conn-dirty-tcp-core:3.2.1-TH2-5001+'
    implementation 'com.exactpro.th2:grpc-lw-data-provider:2.0.0-dev-version-2+'

    implementation 'org.slf4j:slf4j-api'
    implementation 'io.github.microutils:kotlin-logging:3.0.0' // The last version bases on kotlin 1.6.0
    implementation 'org.apache.commons:commons-lang3'

    implementation 'io.netty:netty-all'
    implementation 'com.google.auto.service:auto-service:1.0.1'

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'


    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:1.7.10'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'

    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'
    kapt 'com.google.auto.service:auto-service:1.0.1'
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.conn.dirty.tcp.core.Main'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        // You can adjust the logging level as needed
        exceptionFormat "full"
        showStandardStreams = true
    }
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5
    suppressionFile="suppressions.xml"

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

dependencyLocking {
    lockAllConfigurations()
}